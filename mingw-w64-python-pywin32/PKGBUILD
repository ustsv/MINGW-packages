# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname='pywin32'
url='https://sourceforge.net/projects/pywin32'
pkgdesc='Python for Windows Extensions (mingw-w64)'
license=('PSF')
arch=('any')
pkgver=225
pkgrel=1
makedepends=(${MINGW_PACKAGE_PREFIX}-python2 ${MINGW_PACKAGE_PREFIX}-python2-setuptools
             ${MINGW_PACKAGE_PREFIX}-python3 ${MINGW_PACKAGE_PREFIX}-python3-setuptools
             rsync)
pkgbase=mingw-w64-python-${_realname}
pkgname=(${MINGW_PACKAGE_PREFIX}-python2-${_realname} ${MINGW_PACKAGE_PREFIX}-python3-${_realname})
source=("https://github.com/mhammond/${_realname}/archive/b${pkgver}.zip"
        001-compile-and-setup-fixes.patch
        002-min-max.patch
        003-dependency-fixes.patch)
sha256sums=('4fe036b83caf8cfb1fcbf6cf98b82a11ab1e41d2b652121d0a4395b5e9deeb46'
            '071b119512ee06ea9008b96f83f6a38fdbcd55266758e169da64340861f29c3b'
            '0dc953883a2917f1eddb93f1615db9749b5f758664ec539a4bf2a96f11087524'
            '0013d3bb9b10511f28e5e71fa59b0431445a5cfc5ae7950e33251af14a087f5f')

prepare() {
    cd "${srcdir}/${_realname}-b${pkgver}"
    patch -p1 < "${startdir}"/001-compile-and-setup-fixes.patch
    patch -p1 < "${startdir}"/002-min-max.patch
    patch -p1 < "${startdir}"/003-dependency-fixes.patch
}

build() {
    for _python in python2 python3; do
        msg2 "Synchronizing ${_python} build directory"
        rsync --recursive --times "${srcdir}/${_realname}-b${pkgver}"/* "${srcdir}/build-${_python}-${CARCH}-${_realname}-${pkgver}"
        cd "${srcdir}/build-${_python}-${CARCH}-${_realname}-${pkgver}"
        CFLAGS="-Wno-narrowing -fno-operator-names -fpermissive" \
        LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
        INCLUDE="${MINGW_PREFIX}/include:${MINGW_PREFIX}/${CARCH}-w64-mingw32/include" \
        ${MINGW_PREFIX}/bin/${_python} setup.py build --compiler=mingw32
    done
}

_package() {
    depends=(${MINGW_PACKAGE_PREFIX}-${_python}-setuptools)
    cd "${srcdir}/build-${_python}-${CARCH}-${_realname}-${pkgver}"
    DISTUTILS_DEBUG=1 \
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/${_python} setup.py install \
        --prefix=${MINGW_PREFIX} \
        --root="${pkgdir}" \
        --optimize=1
}

package_mingw-w64-i686-python2-pywin32()   { _python=python2 _package; }
package_mingw-w64-i686-python3-pywin32()   { _python=python3 _package; }
package_mingw-w64-x86_64-python2-pywin32() { _python=python2 _package; }
package_mingw-w64-x86_64-python3-pywin32() { _python=python3 _package; }
